
&НаСервереБезКонтекста
Процедура ЗагрузитьНаСервере(АдресСравненияФайлов, Организация, КонфигурацияИсточник, КонфигурацияПриемник) 
	
	// Выгрузка данных
	
	ФайлПриемник = ПолучитьИмяВременногоФайла("txt");
	ДанныеХранилища = ПолучитьИзВременногоХранилища(АдресСравненияФайлов);
	ДанныеХранилища.Записать(ФайлПриемник);  
	
	СтруктураКодов = Новый Соответствие;
	СтруктураКодов.Вставить("***", Перечисления.ТипыИзменений.ОбъектИзменен);
	СтруктураКодов.Вставить("-->", Перечисления.ТипыИзменений.ОбъектПрисутствуетТолькоВОсновнойКонфигурации);
	СтруктураКодов.Вставить("<--", Перечисления.ТипыИзменений.ОбъектПрисутствуетТолькоВКонфигурацииПоставщика); 
	
	КэшКоличестваПробелов = Новый Соответствие;
	
	Текст = Новый ЧтениеТекста(ФайлПриемник);  
	
	Стр = Текст.ПрочитатьСтроку();
	
	СтрТекущегоОбъектаМетаданных =  Неопределено; 

	СтрокаИзменений = "";
	ЗаписаныСвойства = Ложь;
	КоличествоИзменений = 0;
	ШапкаФайла = Истина;
	СтрокаОписание = "";
	ЭтоОписание = Ложь;
	
	Пока Стр <> Неопределено Цикл 
		
		Стр = Текст.ПрочитатьСтроку();
		
		КоличествоЛидирующихПробелов = ОписаниеСвойствЗаполнение.ВернутьКоличествоЛидирующихПробелов(Стр, КэшКоличестваПробелов); 
		
		Если КоличествоЛидирующихПробелов = 2 Тогда // это новый объектМетаданных
			
			Если НЕ СтрТекущегоОбъектаМетаданных = Неопределено Тогда
				ШапкаФайла = Ложь;
			КонецЕсли;
			Если НЕ СтруктураПустая(СтрТекущегоОбъектаМетаданных) Тогда
				СоздатьОписаниеИзменений(СтрТекущегоОбъектаМетаданных, СтрокаИзменений, Организация, КонфигурацияИсточник, КонфигурацияПриемник);
				КоличествоИзменений = КоличествоИзменений + 1;
			КонецЕсли;
			СтрТекущегоОбъектаМетаданных =  ПолучитьСтруктуруТекущегоОбъектаМетаданных(Стр, СтруктураКодов);
		Продолжить;
			
		КонецЕсли; 
		
		Если НЕ ШапкаФайла Тогда
			СтрокаИзменений = СтрокаИзменений  + ?(ПустаяСтрока(СтрокаИзменений),"",Символы.ПС) + Стр;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ СтруктураПустая(СтрТекущегоОбъектаМетаданных) Тогда
		СоздатьОписаниеИзменений(СтрТекущегоОбъектаМетаданных, СтрокаИзменений, Организация, КонфигурацияИсточник, КонфигурацияПриемник);
		КоличествоИзменений = КоличествоИзменений + 1;
	КонецЕсли;
	
	Сообщить("Обработано " + КоличествоИзменений + " изменений!");
	
КонецПроцедуры 

&НаСервереБезКонтекста
Процедура СоздатьОписаниеИзменений(СтрТекущегоОбъектаМетаданных, СтрокаИзменений, Организация, КонфигурацияИсточник, КонфигурацияПриемник)
	
	НовСпр = Справочники.ОписаниеИзменений.СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(НовСпр, СтрТекущегоОбъектаМетаданных);
	НовСпр.Описание = СтрокаИзменений;
	НовСпр.ДатаЗагрузки = ТекущаяДатаСеанса();
	НовСпр.Организация =  Организация;
	НовСпр.КонфигурацияИсточник = КонфигурацияИсточник;
	НовСпр.КонфигурацияПриемник = КонфигурацияПриемник;
	НовСпр.Наименование =  "" + СтрТекущегоОбъектаМетаданных.ГруппаМетаданных 
		+ " - " +  СтрТекущегоОбъектаМетаданных.Метаданные;
	//НовСпр.Записать();
	ОписаниеСвойствЗаполнение.ЗаполнитьЗначенияСвойствТЧОписаниеСвойств(НовСпр); // заполнение ТЧ ОписаниеСвойств
	СтрокаИзменений = "";
	НовСпр.Записать();
КонецПроцедуры


&НаСервереБезКонтекста
Функция ПолучитьСтруктуруТекущегоОбъектаМетаданных(Стр, СтруктураКодов)
	
	СтруктураОбъекта =  Новый Структура("ТипИзменения, ГруппаМетаданных, Метаданные, СвойствоМетаданных, ЗначениеСвойствМетаданных");
	
	Стр = Прав(Стр, СтрДлина(Стр) - 2);
	Для каждого Код_ Из СтруктураКодов Цикл
		Если НЕ СтрНайти (Стр,Код_.Ключ) = 0 Тогда
			СтруктураОбъекта.Вставить("ТипИзменения", Код_.Значение);
			Стр = СтрЗаменить(Стр, Код_.Ключ ,"");
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Стр = СтрЗаменить(Стр, "-", "");
	Массив = СтрРазделить(Стр, ".");
	
	Если Массив.Количество() = 2 Тогда
		СтруктураОбъекта.Вставить("ГруппаМетаданных", СокрЛП(Массив[0]));
		СтруктураОбъекта.Вставить("Метаданные", СокрЛП(Массив[1])); 		
	ИначеЕсли  Массив.Количество() = 3 Тогда
		СтруктураОбъекта.Вставить("ГруппаМетаданных", СокрЛП(Массив[0]));
		СтруктураОбъекта.Вставить("Метаданные", СокрЛП(Массив[1])); 	
		СтруктураОбъекта.Вставить("СвойствоМетаданных", СокрЛП(Массив[2])); 
	ИначеЕсли Массив.Количество() = 4 Тогда 
		СтруктураОбъекта.Вставить("ГруппаМетаданных", СокрЛП(Массив[0]));
		СтруктураОбъекта.Вставить("Метаданные", СокрЛП(Массив[1])); 	
		СтруктураОбъекта.Вставить("СвойствоМетаданных", СокрЛП(Массив[2]));
		СтруктураОбъекта.Вставить("ЗначениеСвойствМетаданных", СокрЛП(Массив[3])); 
	Иначе
		
		//Сообщить("Ошибка в определении метаданных");
	КонецЕсли;
	
	Возврат СтруктураОбъекта;
	
КонецФункции // ()

&НаСервереБезКонтекста
Функция СтруктураПустая(СтрТекущегоОбъектаМетаданных)
	Если СтрТекущегоОбъектаМетаданных = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	Для Каждого Стр Из СтрТекущегоОбъектаМетаданных Цикл
		Если ТипЗнч(Стр) = Тип("Структура") Тогда
			Если НЕ СтруктураПустая(Стр) Тогда
				Возврат Ложь;
			КонецЕсли;
		Иначе
			Если ЗначениеЗаполнено(Стр.Значение) Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Возврат Истина;
КонецФункции

&НаКлиенте
Процедура Загрузить(Команда)
	Если ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(ПутьКФайлу) Тогда 
		АдресСравненияФайлов = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ПутьКФайлу));
		ЗагрузитьНаСервере(АдресСравненияФайлов, Организация, КонфигурацияИсточник, КонфигурацияПриемник);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПутьКФайлуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;

	Режим = РежимДиалогаВыбораФайла.Открытие;
	ДиалогОткрытия = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытия.Каталог = ПутьКФайлу;
	ДиалогОткрытия.Заголовок = "Укажите файл сравнения"; 
	
	Параметр = "";
	Оповещение = Новый ОписаниеОповещения("ВыборВнешнегоФайла_Завершение", ЭтаФорма, Параметр );
	ДиалогОткрытия.Показать(Оповещение); 

КонецПроцедуры

&НаКлиенте
Процедура ВыборВнешнегоФайла_Завершение(Результат, Параметр) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		ПутьКФайлу = Результат[0];
	КонецЕсли;
	
КонецПроцедуры

